import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

// To make this portable across various dev machines, use the local.properties file
//  to customize locations of SDK, NDK, build tools version, etc
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('sdk.dir')
def ndkDir = properties.getProperty('ndk.dir')
def btVers = properties.getProperty('buildTools.version')

android {
    compileSdkVersion 23
    buildToolsVersion btVers
    defaultConfig {
        applicationId "com.littlefluffytoys.beebdroid"
        targetSdkVersion 23
        minSdkVersion 13
    }
    useLibrary 'org.apache.http.legacy'
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' //set .so files location to libs instead of jniLibs
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    // add a task that calls regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'ndk-build.cmd', '-C', file('src/main').absolutePath
        } else {
            commandLine ndkDir + '/ndk-build', '-C', file('src/main').absolutePath
        }
    }
    // add this task as a dependency of Java compilation
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
        ouya {
            minSdkVersion 14
            dependencies {
                compile 'ouya:ouya-sdk:+' // You can replace "+" with a specific version number
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:support-annotations:23.0.1'
    compile 'com.android.support:support-v13:23.0.1'
    compile 'com.android.support:support-v4:23.0.1'
}

repositories {
    maven {
        url "http://maven.ouya.tv/"
    }
}
